apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply from: 'https://raw.github.com/gist/1527604/4080ebed7b141d10610825aebd70942fcd301105/emma.gradle'
apply plugin: 'findbugs'
apply plugin: 'eclipse-wtp'
apply plugin: 'war'
//apply plugin: 'jetty'


project.group = 'de.prob'
sourceCompatibility = 1.6
def gradle_version = '1.0-rc-3'


project.version = '2.0.0-milestone-5-SNAPSHOT'



repositories {
    maven {
      name "cobra"
      url "http://cobra.cs.uni-duesseldorf.de/artifactory/repo"
    } 
    maven {
    	name "spock snapshot"
    	url "http://oss.sonatype.org/content/repositories/snapshots/"
    } 
}


tasks.withType(FindBugs){
ignoreFailures = true
}

configurations.all {
      resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}


def  ft = findbugsMain.classes
ft.exclude '**/org/codehaus/groovy/tools/shell/*','**/SpockDomWalkerTest.class',"**/de/prob/scripting/Downloader*.class","**/de/prob/scripting/InteractiveCommand*.class"
findbugsMain.classes = files(ft.files) // explicit list of files

def  ftc = findbugsTest.classes
ftc.exclude '**/SpockDomWalkerTest*'
findbugsTest.classes = files(ftc.files) // explicit list of files


sourceSets.main.java.srcDirs = ['src/main/generated'] // ie there's no stand-alone java source
sourceSets.main.groovy.srcDir 'src/main/java' // ie include the 'java' dir as groovy source

def parser_version = '2.4.6-SNAPSHOT'

dependencies {

	groovy 'org.codehaus.groovy:groovy-all:2.0.0' 

	compile 'com.google.guava:guava:r09' // Apache Licence 2.0
	compile 'commons-cli:commons-cli:1.2' // Apache Licence 2.0

	compile 'org.codehaus.groovy:groovy-all:2.0.0' // Apache Licence 2.0
	compile 'org.fusesource.jansi:jansi:1.5' // Apache Licence 2.0
	compile 'jline:jline:0.9.94' // BSD

	compile 'com.google.inject:guice:3.0' // Apache Licence 2.0
	compile 'com.google.inject.extensions:guice-assistedinject:3.0' // Apache Licence 2.0
	compile 'org.slf4j:slf4j-api:1.6.1' // MIT
	compile 'ch.qos.logback:logback-core:0.9.29' // EPL
	compile 'ch.qos.logback:logback-classic:0.9.29' // EPL
	
	compile 'jgrapht:jgrapht:0.8.3' // LGPL <---
	compile 'jgraphx:jgraphx:1.10.1.0' // BSD
	
	compile 'xstream:xstream:1.2.2' // BSD
	compile 'stax:stax:1.2.0_rc2-dev' // Apache Licence 2.0
	compile 'org.codehaus.jettison:jettison:1.3.1' // Apache Licence 2.0
	compile 'emf:ecore:2.3.0' // EPL
	compile 'emf:common:2.3.0' // EPL
	compile 'emf:xmi:2.7.0' // EPL

	compile 'commons-codec:commons-codec:1.6' // Apache Licence 2.0
	compile 'com.google.inject.extensions:guice-servlet:3.0' // Apache Licence 2.0
	compile 'com.google.code.gson:gson:1.7.1' // Apache Licence 2.0

	compile 'org.eclipse.jetty:jetty-io:8.0.0.M3' // EPL
	compile 'org.eclipse.jetty:jetty-server:8.0.0.M3' // EPL
	compile 'org.eclipse.jetty:jetty-servlet:8.0.0.M3' // EPL
	compile 'org.eclipse.jetty:jetty-util:8.0.0.M3' // EPL
	compile 'org.eclipse.jetty:jetty-webapp:8.0.0.M3' // EPL
	compile 'org.mortbay.jetty:jsp-2.1-glassfish:2.1.v20100127' // EPL

    compile group: "de.prob", name: "answerparser", version: parser_version , changing: true
    compile group: "de.prob", name: "bparser", version: parser_version , changing: true
    compile group: "de.prob", name: "cliparser", version: parser_version , changing: true
    compile group: "de.prob", name: "ltlparser", version: parser_version , changing: true
    compile group: "de.prob", name: "parserbase", version: parser_version , changing: true
    compile group: "de.prob", name: "prologlib", version: parser_version , changing: true
    compile group: "de.prob", name: "unicode", version: parser_version , changing: true

	testCompile 'junit:junit:4.8.2'
	testCompile 'org.mockito:mockito-core:1.8.5'
	testCompile 'org.spockframework:spock-core:0.7-groovy-2.0-SNAPSHOT'
	
}

task createBuildConstants  {
	doFirst {
		def buildconstants_class = """
version=${project.version}"""
		File f = file("src/main/resources/build.properties")
		f.delete()
		f <<  buildconstants_class
	}
}

compileJava {
	dependsOn= [createBuildConstants]
}

def listJars() {
	def cp = sourceSets.main.runtimeClasspath
	def deps = cp.collect { it.getName() }
	def jars = deps.findAll {it.endsWith(".jar")}
	def prefix = jars.collect { "lib/"+it }
	return prefix.inject(".") {str, item -> str + " " +item}
}


jar {
	from sourceSets.main.allJava
	manifest {
		attributes 'Main-Class': 'de.prob.Main'
		attributes 'Class-Path': listJars()
	}
 }


task pack(type: Sync) {
    from configurations.runtime
    into "$buildDir/libs/lib"
}

task deploy(dependsOn: [jar, test,javadoc,pack], group: 'Build', type: Zip) {
	description = 'Assembles the uberjar, runs all tests and builds the javadoc.'
	from "$buildDir/libs/"
	doLast {
		println "Created install-zip in build/distributions"
	}
}


test.doFirst {
	systemProperties['integrationtest'] = 'true'
}

javadoc {
	title="ProB Core "+project.version
	options.tags = ["ordered", "model", "generated"]
}

task wrapper(type: Wrapper) {
    gradleVersion = gradle_version
}

def download(address,target) {
	def file = new FileOutputStream(target)
	def out = new BufferedOutputStream(file)
	out << new URL(address).openStream()
	out.close()
}

public  String getProBDirectory() {
		String homedir = System.getProperty("prob.home");
		if (homedir != null)
			return homedir + java.io.File.separator;
		String env = System.getenv("PROB_HOME");
		if (env != null)
			return env + java.io.File.separator;
		return System.getProperty("user.home") + java.io.File.separator + ".prob"
				+ java.io.File.separator;
	}


task downloadCli << {
		os = System.getProperty("os.name")
		if (os.indexOf("nix") < 0 && os.indexOf("nux") < 0)
			System.err.println("\n!!\tError: CLI download for linux only\n")
		dir = getProBDirectory();
		delete file(dir)
	    new File(dir).mkdirs() 
		targetdir = dir
		targetzip = dir+"probcli_linux.zip"
		url = "http://nightly.cobra.cs.uni-duesseldorf.de/cli/probcli_linux.zip"
		download(url,targetzip)
	    FileTree zip = zipTree(targetzip)
	    copy {
		   from zip
		   into targetdir
	    }
		delete file(targetzip)
}


task integrationtests(dependsOn: ['classes','downloadCli'], type:JavaExec){
	main = 'de.prob.Main'
	classpath = sourceSets.main.runtimeClasspath
	args = ['-test','groovyTests']
}

eclipse {
  project { 
 	 name = 'de.prob.core.kernel'
     natures   'org.eclipse.pde.PluginNature' 
     buildCommand   'org.eclipse.pde.ManifestBuilder'
     buildCommand 'org.eclipse.pde.SchemaBuilder'
  }
}





